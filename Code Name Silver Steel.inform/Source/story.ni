"Code Name Silver Steel" by Special Agent

Volume - NOTES

[Headings are organized under "volume", "book", "part", "chapter" or "section"]

[ ENTRANCE ]
[ Infiltrate the building as a AC repair man using a fake ID ]
[ Enter a service entrance to the maintenance level ]
[ You carry a toolbox, containing a suit and screwdriver ]
[ Remove your clothes and wear the suit ]
[ Open a service panel and enter it - auto lead up stairs to the administrative level ]

[ ADMIN LEVEL ]
[ Large cubicled room with workers going about their business.
	A cubicle is nearby.
	A copy machine is nearby.
	A break room is down the hall.
	An elevator is nearby. It requires a security pass to enter.
	]
[ You notice coffee rings on the edges of the copy machine ]
[ Occasionally one of the workers goes to the copy machine, uses it, and returns to their cubicle. You notice a security pass dangling from their belt ]
[ Enter the break room, get a cup, fill it with coffee ]
[ Place the cup on the copy machine ]
[ When the person goes to use the copy machine they knock the coffee over. They curse and fuss over the machine. You notice they don't have the security card on their belt ]
[ Go to the cubicle, take the security pass ]
[ Use the pass on the elevator and enter. It rides you down ]

[ DATA CENTRE ]
[ Rows of server racks interspersed with terminals ]
[ You notice an fire control terminal, and a database terminal ]
[ A technician is busy at the database terminal. They offer detached remarks on interrogation ]
[ Access the fire control terminal, toggle the fire alarm test system ]
[ Red strobe lights flash in the room. The technician exits promptly ]
[ Access the database terminal. Download the files. Upload a payload. ]
[ epilogue ]

	
Volume - STUFF

Book - Things
	
Part - Props for other characters or scenery

The horn-rimmed glasses is a wearable thing. The description is "Tortoise shell pattern frames line the clear lenses."

The paperwork is a thing. It is scenery. It is fixed in place. It is plural-named. The description is "Boilerplate administrativia useless to you cause."

Instead of taking the paperwork, try examining the paperwork.

A coffee pot is scenery. It is fixed in place. The description is "A pot of hot coffee."

Instead of taking a coffee pot, say "Perhaps you mean to [bold type]put coffee into mug[roman type]".



Part - Player Inventory

A job card is a thing. The description is "[one of]This is a forged job card that states you are on-site to repair a faulty air conditioning unit. During the past week you gained access to the company intranet, and forged a matching maintenance record on their systems.[or]A forged job card.[stopping]". 

An overall is a wearable thing. The description is "[if overall is worn by player]This is your disguise. [end if]It is a light brown workman's overall."

A maintenance tag is a thing. The description is "A minimal security pass that entitles you to do maintenance work."

A toolbox is a closed openable container. The description is "It looks just like a metal toolbox, the red paint is even scuffed away along the edges. In reality it is just a metal toolbox, except it contains spy gear. Best you open it in private."

A screwdriver is a thing in the toolbox. The description is "A phillips head screwdriver."

A data disc is a thing in the toolbox. 
A data disc can be full.
The description of a data disc is "A round silvery disc for storing digital data."

A suit is a wearable thing in the toolbox. The description is "A smart gray suit. The perfect attire for a day of espionage."

A coffee mug is a thing. A coffee mug can be full or empty. A coffee mug is empty. The description is "A white unmarked mug.[if A coffee mug is full] It is filled with hot coffee.[end if]"

A security pass is a wearable thing. The description is "A security pass attached to a lanyard. The print on the card reads 'elevator access'."

A security pass unlocks the admin elevator.


Book - People

Part - Player

The player is wearing the overall.
The player is carrying the job card.
The player is carrying a toolbox.

Part - Receptionist

The receptionist is a woman. She is in the Foyer. The description is "A middle-aged woman with red hair, wearing horn-rimmed glasses.
[one of]She catches you looking at her intently. You shift your gaze.[or]Levelling her gaze, she adjusts her glasses.[or][stopping]".

She is wearing the horn-rimmed glasses.	

Understand "woman/girl/lady" as the receptionist.

[ generic answers for all topics ]
Instead of asking the receptionist about something, say "[one of]She pauses as if thinking, and continues typing.[or]She mumbles a distant 'hmm?'[or]'Not now...' she waves you off.[or]'Is that even in your job description?', she remarks snidely.[cycling]"

[ generic replies, to all show and tell ]
Instead of showing something to the receptionist, say "She looks at [the noun], feigning amusement."

Part - Elderly Admin Guy

The elderly man is a man. The description is "He is an elderly man with receding brown hair. He seems preoccupied with his paperwork."

The elderly man can be upset. The elderly man is not upset;

Instead of asking the elderly man about something, say "You are trying to keep a low profile here, and don't wish to strike up a conversation with this man."

Understand "worker" or "workers" as elderly man.

Part - Technician

A technician is a woman. "A technician is here, working on a terminal." The description is "A middle-aged woman wearing a long white coat. Her brown hair is tied up."

Understand "tech" as technician.


Book - Rooms

Part - Foyer
	
The Foyer is a room. "The spacious foyer of the MegaCorp building carries indistinguishable sounds along the sterile tiled floor and angular furniture, giving the impression of cold detachment.

You are standing by the reception desk in the centre of the room. The raised desk shields the small stature of the person behind it, oblivious to your presence.

To the north is an elevator, and a door beside it."

The tiles is scenery in the Foyer. The description is "Large white square tiles cover the foyer floor." 
Understand "floor" as tiles. 

The desk is scenery in the Foyer. The description is "It is a black circular reception desk, the surface is sleek as if resin coated." 

The furniture is scenery in the Foyer. The description is "A low table and some chairs line the west wall." 
Understand "chair/table" as furniture.

The foyer elevator is scenery in the Foyer. The description is "A large and fancy person-carrier with built-in security." 
Understand "lift" as foyer elevator. 

The service entrance is a door. It is scenery. It is north of the Foyer and south of the Maintenance Quarters.
The description is "A white metal door with the words 'maintenance' in bold black letters."
Understand "door" as service entrance.

Part - Maintenance Quarters

The Maintenance Quarters is a room. "The room is well lit, however the grey plastered walls seem to drain the feng shui out of the carefully crafted maintenance quarters.

There is all manner of piping running along the roof. There is a panel in the north wall.

The service passage leads off to the east."

Some piping is scenery in the Maintenance Quarters. The description is "Pipes of various sizes run along the ceiling. You guess they carry water, gas or waste."
Understand "pipes" and "ceiling" as piping.

The access panel is a door. It is scenery.
The description is "A gray steel panel. This one seems to cover a tunnel."
It is north of the Maintenance Quarters and south of Administration.

Some screws are part of the access panel. The description is "Steel pins with spiralling shafts used to fix other things in place. Usually turned by a screwdriver."

Part - Administration Room

Administration is a room. "[if the location is not visited]You are standing against the wall in a large room. The space is divided into a dozen cubicles. Unlike a beehive of activity, most of the suits working here are sitting inside their small cells. The room has the dull hum of a funeral parlour.[paragraph break][end if]
A cubicle is nearby. [if the Breakroom is visited]The Breakroom is to the west[else]There is a doorway to the west[end if], and an elevator to the north."

The copy machine is a supporter in Administration. The description is "It is a chunky piece of machinery with many tiny buttons. You notice coffee-ring stains on top of the machine. Somebody has a bad habit of putting their cups on the machine."

Some buttons are a part of the copy machine. The description is "They are embedded in the copy machine, imprinted with icons of their functions."

Understand "icons" as some buttons.

Some stains are part of the copy machine. The description is "The stains are made prominent by their location: where a cup is most prone to spill."

Instead of taking some stains, say "You are not here to clean the office equipment."
	
Instead of pushing some buttons, say "You refrain from mashing the buttons, it will be fun but it won't help your mission."
	
Instead of switching on some buttons, try pushing some buttons.

A cubicle is an enterable container in Administration. It is scenery. The description is "It is the closest cubicle to the copy machine."

The admin elevator is a closed locked door. It is scenery. It is north of Administration and south of the Data Centre. The printed name is "elevator". The description is "A metal elevator with an electronic access system."

[Through the admin elevator is the Data Centre.]

The paperwork is in the cubicle.

Understand "cubicles" as cubicle.

The elderly man is in the cubicle.

The security pass is in the cubicle.

Part - Break Room

The Breakroom is a room. "You are in a bare room without furniture. You guess MegaCorp doesn't want relaxed employees." It is west of Administration.

The counter is in the Breakroom. "There is a pot of coffee on a counter here." The description is "Wall mounted and serving one very important purpose: to keep the coffee pot off the ground. There is some kind of poster fixed to the side."

A poster is part of the counter. The description is "[italic type]Do not put your coffee mugs on the copier. You will be liable for any damages due to spillage - Management[roman type]"

A coffee pot and a coffee mug is in the breakroom.

Part - Data Centre

Data Centre is a room. "Rows upon rows of server racks fill this room, lengths of cabling run between them like neurons in a brain. The racks are interspersed with computer terminals, these are used to access various systems in the building."

A technician is in the Data Centre.

A strobe light is in the Data Centre. It is scenery. The description is "A red strobe light fitted to the ceiling of the room. [if a strobe light is lit]It is flashing manically[else]It is waiting patiently until the day it is turned on[end if]."

[ helper thing to point examining in the right direction ]
Some terminals are in the Data Centre. It is scenery. The description is "You notice two terminals nearby: a database terminal, and a fire control terminal."

The fire control terminal is in the Data Centre. 
It is fixed in place.

Instead of examining the fire control terminal, say "This terminal controls the fire detection and sprinkler systems of the building. You can [bold type]log on terminal[roman type] to use it."

The database terminal is in the Data Centre. 
It is fixed in place.

Instead of examining the database terminal, say "This terminal controls the information database. You notice a drive bay attached to it. You can [bold type]log on terminal[roman type] to use it."

The drive bay is a container. The description is "A peripheral that is connected to the database terminal, used to copy files on and off the server. It has a slot to insert a data disc."

The drive bay is part of the database terminal.



Volume - GAME LOGIC

Book - Play Begins

When play begins:
	say "A freelance spy is a great job. It pays a lot and you get to travel to new and interesting places. The only downside is that you can die.[paragraph break]Your client's choice currency is blood money, their interest is information. Your goal is to steal important files, and to further your own agenda on the side.[paragraph break]So here you are: standing in the foyer of MegaCorp, dressed to kill... air conditioners? Well every job has boring moments." 
		
Book - Endings

[ going back into the Foyer, while wearing the suit ]
After going to the Foyer when the player is wearing a suit:
	say "You neglectfully walk into [the location] wearing the suit, not an unusual act in itself, except that the receptionist recognizes you and hastily calls for security. You are escorted deep into the confines of the building, never to be heard from again.";
	end the story;

[ inserting the data disc in the drive bay, while the tech is in the room ]
Check inserting the data disc into the drive bay when a technician is in the location:
	say "'What are you doing?', the tech asks you. 'Who are you? Show me your security pass!'[paragraph break]She realizes that the pass does not belong to you, before you can react she trips an alarm. Armed men surround you within seconds and you are led away, never to be heard from again.";
	end the story;

[ opening your toolbox in the foyer - a warning only, not a real ending ]	
Instead of opening the toolbox when the location is the Foyer:
	say "You open your toolbox and the receptionist peers over her horn-rims at the toolbox contents: [a list of thing in the toolbox]. Realizing your mistake, you quickly flip it closed. You can't risk being caught.";
	
[ win ]
Carry out taking the data disc when the data disc is full:
	say "You escape with the data.";
	end the story;




Book - Foyer Logic

[ senses ]
Instead of smelling when the location is the foyer, say "The scent of floral bleach lingers near the floor."

Instead of listening when the location is the foyer, say "You hear the ghostly echoes of unseen busy-bodies elsewhere in the building, interspersed with the clickety-clacks of the receptionist's typing."

Instead of dropping the toolbox when the location is the Foyer, say "You will need your toolbox later."

[ limit overall use ]
Instead of taking off the overall when the location is the Foyer, say "You won't dare remove the overall here in [the location], and risk exposing your... cover."

[ furniture ]		
Instead of taking the tiles, say "You are not here to maintain the floors."

Instead of taking the desk, say "You are not here to swap out their furniture."

Instead of entering the furniture, say "You have no time or reason to take a break here." 

Instead of taking the furniture, say "You are not here to swap out their furniture." 

[ foyer elevator ]
Instead of taking the foyer elevator, say "You are not here to service their elevators."

Instead of opening the foyer elevator, try entering the foyer elevator.

Instead of entering the foyer elevator, say "'That way is not for maintenance', the receptionist says."

[ service entrance ]
Instead of opening the service entrance when the player is not carrying the maintenance tag, say "'Sir!', the receptionist calls to you, 'You can't enter without a maintenance tag.'"

After opening the service entrance when the location is the Foyer, say "The receptionist eyes you from behind her fortress-desk as you open the door."

[ receptionist & job card logic ]
Instead of showing the job card to the receptionist, try giving the job card to the receptionist.

Instead of giving the job card to the receptionist:
	say "She takes the job card from you and scrutinizes it against her records.";
	say "'Fine', she says, handing you a clip-on tag. 'Carry this with you at all times. The maintenance door is there', she points north. 'Have a nice day'.";
	now the receptionist carries the job card;
	now the player carries the maintenance tag.


Book - Maintenance Quarters Logic

Carry out wearing a suit:
	if the player is wearing the overall:
		say "You remove the overall and drop it on the floor.";
		now the overall is in the location;

Instead of opening the access panel:
	if the access panel is closed:
		say "The panel is screwed shut.";
	else:
		say "The panel is already open";

Instead of taking the screws, say "Perhaps you meant to [bold type]turn screws[roman type]?".

[ unscrew the panel when carrying the screwdriver ]
Instead of turning the screws:
	if the access panel is closed:
		if the player is carrying the screwdriver:
			say "You loosen the screws using the screwdriver. The panel swings open.";
			now the access panel is open;
		else:
			say "You don't have any tools to unscrew those.";
	else:
		say "The screws are already loosened."

Understand "unscrew panel" as a mistake("Perhaps you meant to [bold type]turn screws[roman type]?")

[ force going north when entering the access panel, to enforce the going rule ]
Instead of going the access panel, try going north.

[ only exit north when wearing the suit ]
Check going north when the location is the Maintenance Quarters and the access panel is open:
	if the player is wearing a suit:
		say "Deftly, you step through the access tunnel and into the emergency fire escape stairwell. You ascend the stairs in the dim light and emerge surreptitiously in the administrative level of the building. Your arrival goes unnoticed.";
		continue the action;
	else:
		say "You sense that you should change your disguise first.";
		stop the action;


Book - Administration Logic

Instead of listening when the location is Administration, say "The room has the dull hum of a funeral parlour."

Instead of smelling when the location is Administration, say "The scent of coffee seems to be coming from the west."

Instead of going south when the location is Administration, say "You have no reason to go back to the maintenace level now."

Instead of entering a cubicle when the elderly man is in the cubicle, say "Not while the cubicle is occupied."

Instead of taking a security pass when the player is in Administration, say "It is out of reach. You need to enter the cubicle."

Instead of putting a coffee mug on the copy machine when a coffee mug is empty, say "The coffee stains on the copy machine suggest you should fill your mug before trying this."

Instead of putting a coffee mug on the copy machine when a coffee mug is full:
	say "You make sure nobody is watching, and quietly place the mug on the side of the copy machine.";
	move a coffee mug to the copy machine;
	the elderly man makes copies in 1 minute from now;
	
At the time when the elderly man makes copies:
	move elderly man to Administration;
	say "[The elderly man] gets up from his cubicle and walks to the copy machine. He shuffles his papers and pushes the buttons.";
	the copy machine accident happens in 1 minute from now;
	
At the time when copy machine accident happens:
	now a coffee mug is empty;
	now the elderly man is upset;
	say "[italic type]Clang![roman type][line break]'What the hell!' the man curses, trying to save his coffee soaked pages.";
	the elderly man returns in 5 minutes from now;

At the time when the elderly man returns:
	if the location is Administration or the location is a cubicle:
		say "[The elderly man] walks back to his cubicle.";
		move elderly man to the cubicle;
		now the elderly man is not upset;
		if the player is in the cubicle:
			say "You quickly move out of the cubicle to avoid an encounter.";
			move the player to Administration;

Every turn when the location is Administration:
	if the elderly man is upset, say "[one of]'Darn it', the man curses[or]'Rats!' the man mumbles[or]The man shakes his wet pages angrily[at random]."
	
Instead of unlocking the admin elevator with security pass:
	say "You swipe the security pass across the elevator scanner, the door slides open and you step inside. You feel gravity reduce as you descend underground...";
	now the admin elevator is open;
	try going north;
	


Book - Breakroom Logic

Instead of inserting A coffee pot into A coffee mug:
	if the player is not carrying a coffee mug:
		try taking a coffee mug;
	if A coffee mug is empty:
		say "You fill the mug with hot coffee.";
		now the mug is full;
	else:
		say "Your mug overfloweth.";
		
Instead of turning a coffee mug:
	if a coffee mug is empty, say "There are no markings underneath the mug.";
	if a coffee mug is full, say "If you turn the full cup, it will spill.";
	
Book - Data Center

Instead of listening when the location is the Data Centre, say "You hear the low hum of the servers, forever working."

Instead of smelling when the location is the Data Centre, say "The air down here is sterile, constantly cooled and filtered."

Instead of asking a technician about something, say "[The technician] [one of]offers a detached look of boredom and returns to work[or]replies with a muted 'hmm'[at random]."

Check logging on database terminal:
	if a technician is in the location:
		say "You can't use that terminal while the tech is busy on it.";
		stop the action;
		
Carry out logging on fire control terminal:
	if the strobe light is not lit:
		now the technician is nowhere;
		now the strobe light is lit;
		say "[italic type]MegaCorp fire and safety control - Status: no fires - Activating fire drill - Logging off[roman type]...[paragraph break]A siren rings out loudly, accompanied by a flashing red light in the ceiling. The technician runs out through the rows of server racks, and disappears out of sight.";
	else:
		say "[italic type]Status: Drill in progress[roman type]...";
	
Carry out logging on database terminal:
	if the data disc is not in the drive bay:
		say "[italic type]Error - No disc in drive bay[roman type].";
	else:
		now the data disc is full;
		say "You download the files to the data disc in the drive bay, you have what you came for. Now to advance your own agenda: you upload a trojan horse into the server, and log off.";

Every turn when the location is the Data Centre and the strobe light is lit:
	say "[one of]A red light flashes in your eyes.[or]A siren blares.[purely at random]";


Book - Walkthrough

Test walkthrough with "show woman the job card/go n/drop toolbox/open toolbox/take all from toolbox/wear suit/turn the screws/go n/go w/take mug/put coffee in mug/go e/put mug on copy machine/z/go cubicle/take security pass/exit/go n/unlock elevator with security pass/go elevator"

Book - Hacked standard rules

Book - New rules

Understand "log on [something]" as logging on.
Logging on is an action applying to one thing.




Book - Scenes



Volume - FRONT MATTER

Book - title

Book - author

Book - story headline

Book - story description

Book - banner text

After printing the banner text:
	say "[line break]MISSION BRIEFING: Infiltrate MegaCorp under the guise of an air conditioning repair man, steal a security pass, get into the data centre and download the files."


Book - help

Volume - PUBLICATION

Book - release

The story genre is "Fiction". 
The story creation year is 2017. 

Book - addendum